Bauchibred

high

# User's Unclaimed Rewards Could be Lost Due to Position Zeroing Out



## Summary

A potential loss of unclaimed rewards when a position is zeroed out due to a bankrupt bucket within the `PositionManager.memorializePositions` method. In the current implementation, a lender's unclaimed rewards might be forfeited due to the lack of checking and claiming the rewards before zeroing out the LP balance in case of bankruptcy.

## Vulnerability Detail

In the `memorializePositions` method of the `PositionManager` contract, the contract records bucket indexes along with their deposit times and LP balances. This method also transfers LP ownership from the lender to the `PositionManager` contract. During this operation, the contract checks if there was a previous deposit and whether the bucket went bankrupt after that deposit.
Take a look at the [memorializePositions()](https://github.com/sherlock-audit/2023-04-ajna/blob/e2439305cc093204a0d927aac19d898f4a0edb3d/ajna-core/src/PositionManager.sol#L197-L255)

```solidity
    function memorializePositions(
        address pool_,
        uint256 tokenId_,
        uint256[] calldata indexes_
    ) external mayInteract(pool_, tokenId_) override {
        TokenInfo storage tokenInfo = positionTokens[tokenId_];
        EnumerableSet.UintSet storage positionIndexes = tokenInfo.positionIndexes;

        IPool   pool  = IPool(pool_);
        address owner = ownerOf(tokenId_);

        LendersBucketLocalVars memory vars;

        // local vars used in for loop for reduced gas
        uint256 index;
        uint256 indexesLength = indexes_.length;

        // loop through all bucket indexes and memorialize lp balance and deposit time to the Position.
        for (uint256 i = 0; i < indexesLength; ) {
            index = indexes_[i];

            // record bucket index at which a position has added liquidity
            // slither-disable-next-line unused-return
            positionIndexes.add(index);

            (vars.lpBalance, vars.depositTime) = pool.lenderInfo(index, owner);

            // check that specified allowance is at least equal to the lp balance
            vars.allowance = pool.lpAllowance(index, address(this), owner);

            if (vars.allowance < vars.lpBalance) revert AllowanceTooLow();

            Position memory position = tokenInfo.positions[index];

            // check for previous deposits
            if (position.depositTime != 0) {
                // check that bucket didn't go bankrupt after prior memorialization
                if (_bucketBankruptAfterDeposit(pool, index, position.depositTime)) {
                    // if bucket did go bankrupt, zero out the LP tracked by position manager
                    position.lps = 0;
                }
            }

            // update token position LP
            position.lps += vars.lpBalance;
            // set token's position deposit time to the original lender's deposit time
            position.depositTime = vars.depositTime;

            // save position in storage
            tokenInfo.positions[index] = position;

            unchecked { ++i; }
        }

        // update pool LP accounting and transfer ownership of LP to PositionManager contract
        pool.transferLP(owner, address(this), indexes_);

        emit MemorializePosition(owner, tokenId_, indexes_);
    }

```

Most especially at [L231-238](https://github.com/sherlock-audit/2023-04-ajna/blob/e2439305cc093204a0d927aac19d898f4a0edb3d/ajna-core/src/PositionManager.sol#L231-L238)

```solidity
// check for previous deposits
if (position.depositTime != 0) {
  // check that bucket didn't go bankrupt after prior memorialization
  if (_bucketBankruptAfterDeposit(pool, index, position.depositTime)) {
      //@audit
    // if bucket did go bankrupt, zero out the LP tracked by position manager
    position.lps = 0;
  }
}
```

When a bucket is found to be bankrupt after a previous deposit, the contract zeroes out the LPs tracked by the `PositionManager` for that bucket. However, in the current implementation, there's no check or claim for unclaimed rewards before this operation.

In contrast, if you look at the `RewardsManager`, specifically the [claimRewards](https://github.com/sherlock-audit/2023-04-ajna/blob/e2439305cc093204a0d927aac19d898f4a0edb3d/ajna-core/src/RewardsManager.sol#LL112C1-L144C6) function there's no requirement for the bucket to not be bankrupt. Thus, even if a bucket is bankrupt, the lender could still claim rewards, which is reliant on the tracked LP balance in the `PositionManager`.

## Impact

Loss of unclaimed rewards for lenders. When the `PositionManager` zeroes out the LP balance of a bankrupt bucket during a memorialization operation, lenders may inadvertently lose their unclaimed rewards. This is because claiming rewards is reliant on the tracked LP balance, which is zeroed out in this process.

## Code Snippet

[claimRewards](https://github.com/sherlock-audit/2023-04-ajna/blob/e2439305cc093204a0d927aac19d898f4a0edb3d/ajna-core/src/RewardsManager.sol#LL112C1-L144C6)
[L231-238](https://github.com/sherlock-audit/2023-04-ajna/blob/e2439305cc093204a0d927aac19d898f4a0edb3d/ajna-core/src/PositionManager.sol#L231-L238)
[memorializePositions()](https://github.com/sherlock-audit/2023-04-ajna/blob/e2439305cc093204a0d927aac19d898f4a0edb3d/ajna-core/src/PositionManager.sol#L197-L255)

## Tool used

Manual Review

## Recommendation

Introduce a check for unclaimed rewards before the LP balance is zeroed out in the `memorializePositions` method. This could be implemented in the form of a function call to claim any existing rewards before the LP balance is updated. Additionally, adequate warnings or prompts could be provided to inform the lender about unclaimed rewards before proceeding with the operation, thus giving the lender a chance to claim their rewards before their position is updated.
