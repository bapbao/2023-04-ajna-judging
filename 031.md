Bauchibred

medium

# Potential Loss of Bucket Rewards due to Missing Slippage Control in the Staking Function



## Summary

The RewardsManager.sol contract in Ajna Protocol lacks a user-specified slippage mechanism in its staking function (Note that the stake() function unfairly sets it to 100%). This absence could lead to users losing out on their expected profits.

## Vulnerability Detail

In Ajna Protocol's RewardsManager.sol the [stake()](https://github.com/sherlock-audit/2023-04-ajna/blob/e2439305cc093204a0d927aac19d898f4a0edb3d/ajna-core/src/RewardsManager.sol#L146-L206) function is responsible for handling user staking actions. However this function contains a potential flaw due to the lack of user-defined slippage control.
Take a look at the [stake()](https://github.com/sherlock-audit/2023-04-ajna/blob/e2439305cc093204a0d927aac19d898f4a0edb3d/ajna-core/src/RewardsManager.sol#L146-L206) function

```solidity
    /**
     *  @inheritdoc IRewardsManagerOwnerActions
     *  @dev    === Revert on ===
     *  @dev    not owner `NotOwnerOfDeposit()`
     *  @dev    === Emit events ===
     *  @dev    - `Stake`
     */
    function stake(
        uint256 tokenId_
    ) external override {
        address ajnaPool = positionManager.poolKey(tokenId_);


        // check that msg.sender is owner of tokenId
        if (IERC721(address(positionManager)).ownerOf(tokenId_) != msg.sender) revert NotOwnerOfDeposit();


        StakeInfo storage stakeInfo = stakes[tokenId_];
        stakeInfo.owner    = msg.sender;
        stakeInfo.ajnaPool = ajnaPool;


        uint96 curBurnEpoch = uint96(IPool(ajnaPool).currentBurnEpoch());


        // record the staking epoch
        stakeInfo.stakingEpoch = curBurnEpoch;


        // initialize last time interaction at staking epoch
        stakeInfo.lastClaimedEpoch = curBurnEpoch;


        uint256[] memory positionIndexes = positionManager.getPositionIndexes(tokenId_);
        uint256 noOfPositions = positionIndexes.length;
        uint256 bucketId;


        for (uint256 i = 0; i < noOfPositions; ) {
            bucketId = positionIndexes[i];


            BucketState storage bucketState = stakeInfo.snapshot[bucketId];
            // record the number of lps in bucket at the time of staking
            bucketState.lpsAtStakeTime = positionManager.getLP(tokenId_, bucketId);
            // record the bucket exchange rate at the time of staking
            bucketState.rateAtStakeTime = IPool(ajnaPool).bucketExchangeRate(bucketId);


            // iterations are bounded by array length (which is itself bounded), preventing overflow / underflow
            unchecked { ++i; }
        }


        emit Stake(msg.sender, ajnaPool, tokenId_);


        // transfer LP NFT to this contract
        IERC721(address(positionManager)).transferFrom(msg.sender, address(this), tokenId_);


        // calculate rewards for updating exchange rates, if any
        uint256 updateReward = _updateBucketExchangeRates(
            ajnaPool,
            positionIndexes
        );


        // transfer bucket update rewards to sender even if there's not enough balance for entire amount
        _transferAjnaRewards({
            transferAmount_: updateReward,
            minAmount_:      0
        });
    }
```

Key to note that slippage is a common concept in DeFi, referring to the difference between the expected outcome and the actual result of a transaction.

Here is the crucial point of the vulnerability

```solidity
// transfer bucket update rewards to sender even if there's not enough balance for entire amount
_transferAjnaRewards({
transferAmount_: updateReward,
minAmount_: 0
});

```

In this case, when users stake their tokens, the calculated rewards are assumed to be fully paid out due to the hard-coded 100% slippage value. This idealized scenario, however, doesn't always align with reality. If the contract's balance falls short, users will receive a reward lower than the calculated amount, potentially even zero, while the system inaccurately records that the full amount of rewards has been distributed. This discrepancy could result in users unintentionally suffering losses as they may not have opted to stake their tokens had they known they wouldn't receive all of their rewards, or at least the minimum reward amount they would accept even considering slippage (NB: slippage in this case is the user-provided one)

## Impact

The potential impact of this vulnerability is that users may not receive the full rewards they are entitled to according to the system's records. This situation could lead to a trust deficit and financial losses for the users, thus compromising the reliability and fairness of the staking process.

## Code Snippet

[stake()](https://github.com/sherlock-audit/2023-04-ajna/blob/e2439305cc093204a0d927aac19d898f4a0edb3d/ajna-core/src/RewardsManager.sol#L146-L206)

## Tool used

Manual Audit

## Recommendation

A recommended mitigation strategy for this vulnerability is to allow users to specify their acceptable minimum reward amount, or slippage, during the staking process. This modification can replace the current hard-coded 100% slippage, thereby empowering users to define their risk tolerance levels and potential profits. Here's an example of how this can be implemented:

```solidity
function stake(uint256 tokenId_, uint256 minAmount_) external override {
    // ... code omitted for brevity ...

    _transferAjnaRewards({
        transferAmount_: updateReward,
        minAmount_:      minAmount_
    });
}
```

By implementing user-defined slippage, users gain more control over their rewards, ensuring that they can decide the minimum acceptable return for their staking actions, thereby improving the trustworthiness and user-friendliness of the protocol.
