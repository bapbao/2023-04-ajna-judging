Bauchibred

medium

# Hard-coded Slippage Value in Unstaking Function Can Result in Denial-of-Service


## Summary

A hard-coded slippage value in the unstaking function of Ajna Protocol's RewardsManager contract can potentially block users from unstaking their tokens, leading to a Denial-of-Service (DoS). This issue mainly arises when there's a high demand for unstaking or withdrawals or if a user wishes to withdraw a large amount. It is highly recommended that the protocol should allow users to set their own slippage value to prevent any potential DoS.

## Vulnerability Detail

Slippage in DeFi protocols refers to the difference between the expected price of a trade and the actual price at which the trade is executed. Ajna Protocol's RewardsManager contract's `unstake()` function has a hard-coded zero slippage, exarcebating the issue the slippage has been set to "0"
Take a look at the [unstake()](https://github.com/sherlock-audit/2023-04-ajna/blob/e2439305cc093204a0d927aac19d898f4a0edb3d/ajna-core/src/RewardsManager.sol#L216-L223) function, which calls the [`_unstake()`](https://github.com/sherlock-audit/2023-04-ajna/blob/e2439305cc093204a0d927aac19d898f4a0edb3d/ajna-core/src/RewardsManager.sol#L763-L814) function

```solidity
    function unstake(
        uint256 tokenId_
    ) external override {
        _unstake({
            tokenId_:      tokenId_,
            claimRewards_: true
        });
    }


    function _unstake(uint256 tokenId_, bool claimRewards_) internal {
        StakeInfo storage stakeInfo = stakes[tokenId_];


        if (msg.sender != stakeInfo.owner) revert NotOwnerOfDeposit();


        address ajnaPool = stakeInfo.ajnaPool;
        uint256 rewardsEarned;


        // gracefully unstake, claim rewards if any
        if (claimRewards_) {
            rewardsEarned = _calculateAndClaimAllRewards(
                stakeInfo,
                tokenId_,
                IPool(ajnaPool).currentBurnEpoch(),
                false,
                ajnaPool
            );
        }


        // remove bucket snapshots recorded at the time of staking
        uint256[] memory positionIndexes = positionManager.getPositionIndexes(tokenId_);
        uint256 noOfIndexes = positionIndexes.length;


        for (uint256 i = 0; i < noOfIndexes; ) {
            delete stakeInfo.snapshot[positionIndexes[i]]; // reset BucketState struct for current position


            unchecked { ++i; }
        }


        // remove recorded stake info
        delete stakes[tokenId_];


        emit Unstake(msg.sender, ajnaPool, tokenId_);


        // gracefully unstake, transfer rewards to claimer ensuring entire amount
        if (claimRewards_) {
                //@audit Hardcoded slippage value would cause DOS to users while unstaking if rewardsEarned > is greater than contract's Ajna balance but user is ready to forfeit part of the rewards and receive a reward < the balance
            _transferAjnaRewards({
                transferAmount_: rewardsEarned,
                minAmount_:      rewardsEarned
            });
        }


        // transfer LP NFT from contract to sender
        IERC721(address(positionManager)).transferFrom(address(this), msg.sender, tokenId_);
    }
```

In cases where a lot of users are trying to unstake or withdraw, or when a single user tries to withdraw a large amount, a hard-coded zero slippage could prevent the transaction from going through if the available balance in the contract is less than the calculated rewards. This could potentially block users from unstaking their tokens and lead to a Denial-of-Service (DoS).

### Hypothetical POC

Consider a scenario where the balance of the contract is 1,200 tokens. A user tries to unstake, and their reward is calculated as 1,300 tokens. The user is prepared to accept a 10% slippage as he understands, which would result in a reward of 1,170 tokens. Given that this is less than the contract balance, the transaction should go through in normal circumstances but there is no availability for user to provide his acceptable minAmount so users rewards get stucked in the contract

Note that in this instance, with a hardcoded zero slippage, the transaction reverts because the reward is greater than the available balance. Therefore, even though the user is willing to accept a lower amount due to slippage, they are denied the opportunity to unstake their tokens. This could lead to a potential DoS under high volatility scenarios.

## Impact

This flaw could potentially block a large number of users from unstaking their tokens especially when there's a high demand for unstaking or withdrawals. It restricts users from controlling their assets, possibly leading to financial losses due to missed market opportunities. Therefore, the severity of this vulnerability is considered high.

## Code Snippet

[unstake()](https://github.com/sherlock-audit/2023-04-ajna/blob/e2439305cc093204a0d927aac19d898f4a0edb3d/ajna-core/src/RewardsManager.sol#L216-L223) and [`_unstake()`](https://github.com/sherlock-audit/2023-04-ajna/blob/e2439305cc093204a0d927aac19d898f4a0edb3d/ajna-core/src/RewardsManager.sol#L763-L814)

## Tool used

Manual Audit

## Recommendation

To mitigate this issue, the protocol should allow users to specify their acceptable slippage when unstaking, replacing the hard-coded zero slippage. Here's an example of how the new code might look:

```solidity
function unstake(uint256 tokenId_, uint256 minAmount_) external override {
    _unstake({
        tokenId_:      tokenId_,
        minAmount_:    minAmount_,
        claimRewards_: true
    });
}

function _unstake(uint256 tokenId_, uint256 minAmount_, bool claimRewards_) internal {
    // ... omitted for brevity ...

    if (claimRewards_) {
        _transferAjnaRewards({
            transferAmount_: rewardsEarned,
            minAmount_:      minAmount_
        });
    }
}
```

By allowing users to set their own slippage value, they would have more control over their assets and prevent potential DoS, especially during times when there's a high demand for unstaking or withdrawals.
